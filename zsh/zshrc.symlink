#!/usr/local/bin/zsh

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

export CODE=$HOME/code #c<tab>
export DOTFILES=$ZSH #d<tab>

# use .localrc for SUPER SECRET STUFF that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# setup chruby
source /usr/local/share/chruby/chruby.sh
source /usr/local/share/chruby/auto.sh
RUBIES=(~/.rubies/*)
chruby ruby-2.1.3
# check for bundler, if it doesn't exist, install it
# (gem list | grep bundler) || gem install bundler

# setup nvm
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm

# setup hub (aliased to git)
eval "$(hub alias -s)"

# setup docker
export DOCKER_CERT_PATH=$HOME/.boot2docker/certs/boot2docker-vm
export DOCKER_TLS_VERIFY=1
export DOCKER_HOST=tcp://192.168.59.103:2376 # modify to correct IP/port $(boot2docker ip)

# setup fzf
# source ~/.fzf.zsh
# Setting ag as the default source for fzf
# export FZF_DEFAULT_COMMAND='ag -l -g ""'

# ===========================================================================
# STOP!!!
# NOTE: DO NOT PUT `source`s for *.zsh files local to ~/.dotfiles/zsh folder.
# We already source these with the glob pattern around L16.
# ===========================================================================
source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

bindkey -e
