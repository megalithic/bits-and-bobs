#!/usr/bin/sh
#
# There are different zsh shell modes, and files that manage those:
#
# .zshenv: (always) <-- vim responds to stuff in here
# .zprofile: [[ -o login ]] <-- vim also responds to things here
# .zshrc: [[ -o interactive ]]
# .zlogin: [[ -o login ]]
#

# To profile stuff for zsh startup, uncomment:
# zmodload zsh/zprof

bindkey -e

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export DOTFILES=$HOME/.dotfiles

# use .localrc for SUPER SECRET STUFF that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load only the completion files after everything else
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# NOTE: if you have issues with `complete:13: command not found: compdef`,
# try this https://gist.github.com/rbellamy/4878fa9c5f7c2805384b

unset config_files

# color stuffs
eval `gdircolors $HOME/.dotfiles/zsh/dircolors.ansi-dark`

# Fallback to built in ls colors
# zstyle ':completion:*' list-colors ''
# ref: https://github.com/robbyrussell/oh-my-zsh/issues/1563#issuecomment-53638038
zstyle ':completion:*:default' list-colors "${(@s.:.)LS_COLORS}"

# asdf
# source $HOME/.asdf/asdf.sh
# source $HOME/.asdf/completions/asdf.bash

# setup chruby
# source /usr/local/share/chruby/chruby.sh
# source /usr/local/share/chruby/auto.sh
# RUBIES=(~/.rubies/*)

# RBEnv
eval "$(rbenv init --no-rehash -)"
(rbenv rehash &) 2> /dev/null

# setup n
export N_PREFIX="$HOME/n"; [[ :$PATH: == *":$N_PREFIX/bin:"* ]] || PATH+=":$N_PREFIX/bin"  # Added by n-install (see http://git.io/n-install-repo).
[[ -s "$HOME/.avn/bin/avn.sh" ]] && source "$HOME/.avn/bin/avn.sh" # load avn

# setup fzf
# consider these handy fzf functions: https://github.com/junegunn/dotfiles/blob/master/bashrc#L267
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
# these must be set *after* loading fzf:
export FZF_COMPLETION_TRIGGER=''
bindkey '^G' fzf-completion
bindkey '^I' $fzf_default_completion

# z.sh init
source ~/.dotfiles/bin/z.sh

# ===========================================================================
# STOP!!!
# NOTE: DO NOT PUT `source`s for *.zsh files local to ~/.dotfiles/zsh folder.
# We already source these with the glob pattern around L18.
# ===========================================================================
source /usr/local/share/zsh-autosuggestions/zsh-autosuggestions.zsh
if [[ $ZSH_EVAL_CONTEXT == 'file' ]]; then
  source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

export PATH="$HOME/.yarn/bin:$HOME/.config/yarn/global/node_modules/.bin:$PATH"
